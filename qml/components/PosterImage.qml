import Felgo 4.0
import QtQuick 2.15
import Qt5Compat.GraphicalEffects


Item {
    id: posterImage

    // Propri√©t√©s publiques
    property string source: ""
    property alias fillMode: image.fillMode
    property bool asynchronous: true  // chargement asynchrone: l'image se charge en arri√®re-plan
    property real borderRadius: dp(6)

    // Permet d'activer ou non le lazy loading
    property bool enableLazyLoading: false  // D√©sactiv√© par d√©faut pour ne pas casser l'existant

    // Indisuq si l'image est visible dans la GridView
    property bool isVisible: true           // Sera contr√¥l√© par le parent (GridView)
    property real visibilityThreshold: 50   // Distance en pixels avant de charger

    // Propri√©t√© calcul√©e pour d√©cider du chargement (si on doit charger
    // r√©ellement l'image (true si pas de lazy loading ou si l'image est visible)
    readonly property bool shouldLoad: !enableLazyLoading || isVisible

    // Propri√©t√©s de statut (lecture seule)
    readonly property alias status: image.status
    readonly property alias progress: image.progress
    readonly property bool isLoading: image.status === Image.Loading
    readonly property bool hasError: image.status === Image.Error
    readonly property bool isReady: image.status === Image.Ready

    Component.onCompleted: {
        console.log("PosterImage initialis√© pour:", source, " - lazy loading:", enableLazyLoading, " visible:", isVisible)
    }

    // Image principale
    Image {
        id: image
        anchors.fill: parent

        // Source conditionnelle selon lazy loading
        source: posterImage.shouldLoad ? posterImage.source : ""

        asynchronous: posterImage.asynchronous
        fillMode: Image.PreserveAspectCrop  // Maintient les proportions de l'image

        // Optimisation taille pour √©viter la consommation excessive de m√©moire
        sourceSize.width: Math.min(width * 2, 400) // Limite √† 400px max, retina-ready
        sourceSize.height: Math.min(height * 2, 600)

        visible: isReady   // Masquer l'image pendant chargement ou si erreur

        // Coins arrondis avec masque (Image ne supporte pas nativement la propir√©t√© radius)
        layer.enabled: true  // pour appliquer les effets sur les √©l√©ments Qt
        layer.effect: OpacityMask {
            maskSource: Rectangle {
                width: image.width
                height: image.height
                radius: posterImage.borderRadius
            }
        }

        onStatusChanged: {
            switch (status) {
                case Image.Ready:
                    console.log("‚úÖ Image charg√©e:", posterImage.source,
                               "Taille rendu:", width + "x" + height,
                               "SourceSize:", sourceSize.width + "x" + sourceSize.height)
                    break
                case Image.Error:
                    console.log("‚ùå Erreur image:", posterImage.source)
                    break
                case Image.Loading:
                    console.log("‚è≥ Chargement:", posterImage.source)
                    break
            }
        }

        onProgressChanged: {
            if (status === Image.Loading && progress > 0) {
                console.log("üìä Progression:", Math.round(progress * 100) + "%", source)
            }
        }
    }

    // Placeholder pendant le chargement
    Rectangle {
        id: placeholder
        anchors.fill: parent
        radius: borderRadius

        // placeholder visible si image en t√©l√©chargement ou source pas d√©fini
        visible: (isLoading || source === "") || (enableLazyLoading && !shouldLoad)

        // Couleur diff√©rente pour lazy loading vs chargement
        color: {
            if (enableLazyLoading && !shouldLoad) {
                return "#e8e8e8"  // Gris plus fonc√© pour lazy loading
            } else {
                return "#f0f0f0"  // Gris standard pour chargement (fond gris clair uniformis√©)
            }
        }

        // Ic√¥ne cin√©ma
        AppIcon {
            anchors.centerIn: parent

            // Ic√¥ne oeil pour les images en attente de lazy loading
            iconType: enableLazyLoading && !shouldLoad ? IconType.eye : IconType.film
            size: Math.min(parent.width * 0.3, dp(32))  // afin d'√©viter les ic√¥nes trop grandes
            color: enableLazyLoading && !shouldLoad ? "#999999" : "#bdbdbd"
            z: 2  // Ic√¥ne cin√©ma par-dessus le shimmer
        }

        // ‚úÖ Indicateur lazy loading
        AppText {
            anchors.bottom: parent.bottom
            anchors.right: parent.right
            anchors.margins: dp(4)
            text: enableLazyLoading && !shouldLoad ? "üí§" : ""
            font.pixelSize: sp(12)
            visible: enableLazyLoading && !shouldLoad
        }

        // Animation de chargement (shimmer)
        Rectangle {
            id: shimmer  // shimmer :  effet de "brillance" qui traverse l'√©l√©ment
            width: parent.width * 0.6  // 60% de la largeur seulement
            height: parent.height
            radius: parent.radius
            visible: isLoading && shouldLoad  // Pas de shimmer si lazy loading inactif (sur les images en attente)
            z: 1

            gradient: Gradient {  // Transparent -> Blanc semi-transparent -> Transparent
                orientation: Gradient.Horizontal
                GradientStop { position: 0.0; color: "transparent" }
                GradientStop { position: 0.2; color: Qt.rgba(1,1,1,0.3) }  // blanc √† 30% d'opacit√©
                GradientStop { position: 0.4; color: Qt.rgba(1,1,1,0.8) }
                GradientStop { position: 0.5; color: Qt.rgba(1,1,1,1.0) }  // Blanc pur
                GradientStop { position: 0.6; color: Qt.rgba(1,1,1,0.8) }
                GradientStop { position: 0.8; color: Qt.rgba(1,1,1,0.3) }
                GradientStop { position: 1.0; color: "transparent" }
            }

            // Effet de flou pour adoucir
            layer.enabled: true
            layer.effect: FastBlur {
                radius: 4
            }

            // A d√©comenter si on veut un effet plus marqu√©
            // // Bordure brillante pour accentuer l'effet
            // border.width: 1
            // border.color: Qt.rgba(1,1,1,0.5)

            // // Ombre pour plus de profondeur
            // layer.enabled: true
            // layer.effect: DropShadow {
            //     horizontalOffset: 0
            //     verticalOffset: 0
            //     radius: 6
            //     samples: 13
            //     color: Qt.rgba(1,1,1,0.3)
            // }

            PropertyAnimation on x {
                // animation uniquement quand n√©cessaire (placeholder visible et image en cours de chargement et lazy loading actif)
                running: placeholder.visible && isLoading && shouldLoad  // Chargement actif -> affichage du shimmer)
                from: -shimmer.width * 0.25  // Commence √† moiti√© cach√©  (// bas√© sur la largeur du composant)
                to: placeholder.width   // sort compl√®tement √† droite
                // from: 0   // ‚Üê Bord gauche
                // to: placeholder.width - shimmer.width         // ‚Üê Bord droit
                duration: 1000  // Plus rapide -> plus visible
                loops: Animation.Infinite   // l'animation continue jusqu'√† ce que l'image soit charg√©e
                easing.type: Easing.Linear  // Mouvement constant

                onRunningChanged: {
                    if (running) {
                        console.log("‚ú® Shimmer d√©marr√© pour:", posterImage.source, " - largeur: ", shimmer.width)
                    } else {
                        console.log("üõë Shimmer arr√™t√© pour:", posterImage.source)
                    }
                }

                // Debug des conditions
                Component.onCompleted: {
                    console.log("üîç Conditions shimmer:")
                    console.log("  - placeholder.visible:", placeholder.visible)
                    console.log("  - isLoading:", isLoading)
                    console.log("  - running:", running)
                }
            }
        }

        // Debug sur les conditions de visibilit√© du placeholder
        onVisibleChanged: {
            console.log("üì¶ Placeholder visible:", visible)
        }
    }

    // Fallback en cas d'erreur
    Rectangle {
        id: errorFallback
        anchors.fill: parent
        radius: borderRadius
        visible: hasError
        color: "#ffebee"          // Rose tr√®s p√¢le
        border.color: "#ffcdd2"   // Rose clair
        border.width: 1

        Column {
            anchors.centerIn: parent
            spacing: dp(8)  // standard d'espacement moyen

            AppIcon {
                anchors.horizontalCenter: parent.horizontalCenter
                iconType: IconType.exclamationtriangle
                size: dp(24)
                color: "#f44336"
            }

            AppText {
                anchors.horizontalCenter: parent.horizontalCenter
                text: "Image indisponible"
                font.pixelSize: sp(10)
                color: "#666"
                horizontalAlignment: Text.AlignHCenter
            }

            AppText {
                anchors.horizontalCenter: parent.horizontalCenter
                text: "Toucher pour r√©essayer"
                font.pixelSize: sp(7)
                color: "#999"
                horizontalAlignment: Text.AlignHCenter
            }
        }

        // Possibilit√© de retry (zone cliquable qui prend toute la zone)
        MouseArea {
            anchors.fill: parent
            onClicked: {
                console.log("üîÑ Retry demand√© pour:", posterImage.source)

                // Technique de reset
                var originalSource = posterImage.source
                image.source = ""   // reset de l'√©tat de l'image
                // image.source = posterImage.source
                Qt.callLater(function() {
                    image.source = originalSource  // d√©clenchement d'un nouveau chargement
                })
            }
        }
    }

}

